plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'io.freefair.lombok' version '6.4.3.1'
    id 'java'
}

group = 'com.packt.modern.api'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '18'

swaggerSources {
    eStore {
        def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
        def configJson = "${rootDir}/src/main/resources/api/config.json"
        inputFile = file(apiYaml)
        def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
        code {
            language = 'spring'
            configFile = file(configJson)
            rawOptions = ['--ignore-file-override', ignoreFile]
            components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
            dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.eStore.code
sourceSets.main.java.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.eStore.code.outputDir}/src/main/resources"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // open api
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.0.0'
    compileOnly 'io.swagger.core.v3:swagger-annotations:2.2.1'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

    // database drivers
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:2.7.6'

    // flyway
    implementation 'org.flywaydb:flyway-core:7.15.0'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation "io.rest-assured:rest-assured:4.4.0"
    testImplementation 'io.rest-assured:spring-mock-mvc:5.1.1'
}


test {
    useJUnitPlatform()
}
